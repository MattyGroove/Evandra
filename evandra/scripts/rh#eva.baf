// Planar Sphere

IF
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  OR(6)
    Global("rh#EvaLeavesSphere","GLOBAL",1)
    Global("rh#EvaPostLavok","GLOBAL",2)
    Global("rh#EvaPostLavok","GLOBAL",4)
    Global("rh#EvaPostRibald","GLOBAL",2)
    Global("rh#EvaPostPrelate","GLOBAL",2)
    Global("rh#EvaSphereRest","GLOBAL",1)
THEN
RESPONSE #100
    StartDialogueNoSet(Player1)
END

IF
  PartyHasItem("MISC6M")
  AreaCheck("AR0414")
  Global("rh#HeartFiend","AR0414",0)
  CombatCounter(0)
  !See([ENEMY])
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@2 /* The heart of a fiend... strange to think that something so small would possess such power. I'm almost tempted to go seeking a second. */)
    SetGlobal("rh#HeartFiend","AR0414",1)
END

IF
    !InParty("rh#Eva")
    Global("rh#EvaLeavesSphere","GLOBAL",0)
    !Global("rh#EvaPostLavok","GLOBAL",3)
    AreaCheck("AR0411")
    Range([PC],30)
    Dead("Lavok02")
THEN
RESPONSE #100
    SetGlobal("rh#EvaLeavesSphere","GLOBAL",1)
END

IF
    InParty("rh#Eva")
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    Global("rh#EvaPostLavok","GLOBAL",0)
    !Global("rh#EvaLeavesSphere","GLOBAL",2)
    Dead("Lavok02")
    See(Player1)
THEN
RESPONSE #100
    SetGlobal("rh#EvaPostLavok","GLOBAL",1)
    RealSetGlobalTimer("rh#EvaTalksTimer","GLOBAL",2)
END

IF
  InParty("rh#Eva")
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  Global("rh#EvaReynaTalk","GLOBAL",0)
  Global("rh#MoveKnights","GLOBAL",0)
  !Global("rh#EvaLeavesSphere","GLOBAL",2)
  Dead("Lavok02")
  AreaCheck("AR0400")
THEN
RESPONSE #100
    SetGlobal("rh#MoveKnights","GLOBAL",1)
END

IF
    InParty("rh#Eva")
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    Global("rh#EvaPostLavok","GLOBAL",1)
    !Global("rh#EvaLeavesSphere","GLOBAL",2)
    Dead("Lavok02")
    RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
THEN
RESPONSE #100
    SetGlobal("rh#EvaPostLavok","GLOBAL",2)
END

IF    
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  Global("rh#EvaPostRibald","GLOBAL",1)
  AreaCheck("AR0700")
THEN
RESPONSE #100
  SetGlobal("rh#EvaPostRibald","GLOBAL",2)
END

IF
  Global("rh#EvaPostRibald","GLOBAL",1)    
  OR(2)
    !InParty("rh#Eva")
    StateCheck("rh#Eva",CD_STATE_NOTVALID)
THEN
RESPONSE #100
  SetGlobal("rh#EvaPostRibald","GLOBAL",3)
END

IF    
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  Global("rh#EvaPostPrelate","GLOBAL",1)
  AreaCheck("AR0900")
THEN
RESPONSE #100
  SetGlobal("rh#EvaPostPrelate","GLOBAL",2)
END

IF
Global("rh#EvaSphereRest","GLOBAL",0)
Global("PCSphere","GLOBAL",1)
InParty("rh#Eva")
!StateCheck("rh#Eva",CD_STATE_NOTVALID)
AreaCheck("AR0411")
PartyRested()
THEN
RESPONSE #100
    SetGlobal("rh#EvaSphereRest","GLOBAL",1)
END

// ---------------------------------------------
// Romance Check
// ---------------------------------------------

IF
    Global("rh#EvaRomanceCheck","GLOBAL",0)
    Gender(Player1,MALE)
    Race(Player1,ELF)
    CheckStatGT(Player1,11,INT)
THEN
RESPONSE #100
    SetGlobal("rh#EvaRomanceCheck","GLOBAL",1)
END

IF
    Global("rh#EvaRomanceCheck","GLOBAL",0)
THEN
RESPONSE #100
    SetGlobal("rh#EvaRomanceCheck","GLOBAL",2)
END

// ---------------------------------------------
// Anomen Check
// ---------------------------------------------

IF
Global("rh#EvaAnomenCheck","GLOBAL",0)
Alignment("Anomen",LAWFUL_NEUTRAL)
THEN
RESPONSE #100
    SetGlobal("rh#EvaAnomenCheck","GLOBAL",1)
END

// ---------------------------------------------
// Dialogue Track
// ---------------------------------------------

IF
    InParty("rh#Eva")
    Global("SolamnicPrelate","GLOBAL",2)
    Global("rh#EvaTalks","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaTalksTimer","GLOBAL",900)
    SetGlobal("rh#EvaFriendshipActive","GLOBAL",1)
    SetGlobal("rh#EvaTalks","GLOBAL",1)
END

IF  
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
    Global("rh#EvaFriendshipActive","GLOBAL",1)
    OR(16)
      Global("rh#EvaTalks","GLOBAL",2) // talk 1
      Global("rh#EvaTalks","GLOBAL",4) // talk 2
      Global("rh#EvaTalks","GLOBAL",6) // talk 3
      Global("rh#EvaTalks","GLOBAL",8) // talk 4
      Global("rh#EvaTalks","GLOBAL",10) // talk 5
      Global("rh#EvaTalks","GLOBAL",12) // talk 6
      Global("rh#EvaTalks","GLOBAL",14) // talk 7
      Global("rh#EvaTalks","GLOBAL",16) // talk 8
      Global("rh#EvaTalks","GLOBAL",18) // talk 9
      Global("rh#EvaTalks","GLOBAL",20) // talk 10
      Global("rh#EvaTalks","GLOBAL",22) // talk 11
      Global("rh#EvaTalks","GLOBAL",26) // talk 13
      Global("rh#EvaSpellholdTalk","GLOBAL",2)
      Global("rh#EvaUnderdarkTalk","GLOBAL",2)
      Global("rh#EvaVeldrinTalk","GLOBAL",2)
      Global("rh#EvaSuldaTalk","GLOBAL",2)
THEN
  RESPONSE #100
  PlaySong(0)
  PlaySound("rh#esong")
  Dialogue(Player1)
END

IF  
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
    Global("rh#EvaRomanceActive","GLOBAL",1)
    OR(7)
      Global("rh#EvaTalks","GLOBAL",28) // talk 14
      Global("rh#EvaTalks","GLOBAL",30) // talk 15
      Global("rh#EvaTalks","GLOBAL",32) // talk 16
      Global("rh#EvaTalks","GLOBAL",34) // talk 17
      Global("rh#EvaTalks","GLOBAL",36) // talk 18
      Global("rh#EvaTalks","GLOBAL",38) // talk 19
      Global("rh#EvaTalks","GLOBAL",40) // talk 20
THEN
  RESPONSE #100
  PlaySong(0)
  PlaySound("rh#esong")
  Dialogue(Player1)
END

// 1-3, 6-11, 13

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  !AreaType(DUNGEON)
  GlobalLT("chapter","GLOBAL",%bg2_chapter_4%)
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaFriendshipActive","GLOBAL",1)
  OR(10)
    Global("rh#EvaTalks","GLOBAL",1) // 1
    Global("rh#EvaTalks","GLOBAL",3) // 2
    Global("rh#EvaTalks","GLOBAL",5) // 3
    Global("rh#EvaTalks","GLOBAL",11) // 6
    Global("rh#EvaTalks","GLOBAL",13) // 7
    Global("rh#EvaTalks","GLOBAL",15) // 8
    Global("rh#EvaTalks","GLOBAL",17) // 9
    Global("rh#EvaTalks","GLOBAL",19) // 10
    Global("rh#EvaTalks","GLOBAL",21) // 11
    Global("rh#EvaTalks","GLOBAL",25) // 13
THEN
  RESPONSE #100
    IncrementGlobal("rh#EvaTalks","GLOBAL",1)
END

// 4. Messenger

IF
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  OR(6)
        AreaCheck("AR0020")
        AreaCheck("AR0300")
        AreaCheck("AR0400")
        AreaCheck("AR0500")
        AreaCheck("AR0700")
        AreaCheck("AR0900")
  GlobalLT("chapter","GLOBAL",%bg2_chapter_4%)
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaFriendshipActive","GLOBAL",1)
  Global("rh#EvaTalks","GLOBAL",7)
  Global("rh#EvaMessengerSpawn","GLOBAL",0)
THEN
  RESPONSE #100
    CreateCreatureObjectOffset("rh#evmes",Myself,[-15.-15])
    RealSetGlobalTimer("rh#EvaMessengerRespawnTimer","GLOBAL",300)
    SetGlobal("rh#EvaMessengerSpawn","GLOBAL",1)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("rh#EvaMessengerRespawnTimer","GLOBAL")
  Global("rh#EvaMessengerSpawn","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("rh#EvaMessengerSpawn","GLOBAL",0)
END

IF
  InParty(Myself)
  See(Player1)
  !StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  CombatCounter(0)
  !See([ENEMY])
  Global("rh#EvaMessengerSpawn","GLOBAL",2)
  Global("rh#EvaTalks","GLOBAL",7)   
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaTalksTimer","GLOBAL",2) 
    IncrementGlobal("rh#EvaTalks","GLOBAL",1)
END

// 5.

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
    OR(6)
        AreaCheck("AR0020")
        AreaCheck("AR0300")
        AreaCheck("AR0400")
        AreaCheck("AR0500")
        AreaCheck("AR0700")
        AreaCheck("AR0900")
  GlobalLT("chapter","GLOBAL",%bg2_chapter_4%)
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaFriendshipActive","GLOBAL",1)
  Global("rh#EvaTalks","GLOBAL",9)
THEN
  RESPONSE #100
    IncrementGlobal("rh#EvaTalks","GLOBAL",1)
END

// 14.

IF
    InParty(Myself)
    AreaCheck("AR1600")
    Global("rh#EvaRomanceActive","GLOBAL",1)
    Global("rh#EvaTalks","GLOBAL",27)
    Global("rh#EvaBrynnlawTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaTalksTimer","GLOBAL",450)
    SetGlobal("rh#EvaBrynnlawTalk","GLOBAL",1)
END

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  !AreaType(DUNGEON)
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaTalks","GLOBAL",27)
  Global("rh#EvaBrynnlawTalk","GLOBAL",1)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaTalks","GLOBAL",1)
END

// 15. Triggers in Spellhold

IF
    InParty(Myself)
    AreaCheck("AR1512")
    Global("rh#EvaRomanceActive","GLOBAL",1)
    Global("rh#EvaSpellholdTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaTalksTimer","GLOBAL",300)
    IncrementGlobal("rh#EvaSpellholdTalk","GLOBAL",1)
END

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  Global("rh#EvaTalks","GLOBAL",29)
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaSpellholdTalk","GLOBAL",1)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaTalks","GLOBAL",1)
  SetGlobal("rh#EvaSpellholdTalk","GLOBAL",3)
END

// 16, 18-19

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaRomanceActive","GLOBAL",1)
  OR(3)
    Global("rh#EvaTalks","GLOBAL",31)
    Global("rh#EvaTalks","GLOBAL",35)
    Global("rh#EvaTalks","GLOBAL",37)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaTalks","GLOBAL",1)
END

// 17. Triggers in Underdark

IF
    InParty(Myself)
    AreaCheck("AR2100")
    Global("rh#EvaFriendshipActive","GLOBAL",1)
    Global("rh#EvaUnderdarkTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaUnderdarkTimer","GLOBAL",450)
    SetGlobal("rh#EvaUnderdarkTalk","GLOBAL",1)
END

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  RealGlobalTimerExpired("rh#EvaUnderdarkTimer","GLOBAL")
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaUnderdarkTalk","GLOBAL",1)
  Global("rh#EvaTalks","GLOBAL",33)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaTalks","GLOBAL",1)
  SetGlobal("rh#EvaUnderdarkTalk","GLOBAL",3)
END

// 20. Triggers after becoming drow

IF
    InParty(Myself)
    Global("PlayerLooksLikeDrow","GLOBAL",1)
    Global("rh#EvaFriendshipActive","GLOBAL",1)
    Global("rh#EvaPhilomenaTalks","GLOBAL",7)
    Global("rh#EvaVeldrinTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaVeldrinTimer","GLOBAL",450)
    SetGlobal("rh#EvaVeldrinTalk","GLOBAL",1)
END

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  Global("PlayerLooksLikeDrow","GLOBAL",1)
  RealGlobalTimerExpired("rh#EvaVeldrinTimer","GLOBAL")
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaVeldrinTalk","GLOBAL",1)
  Global("rh#EvaTalks","GLOBAL",39)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaTalks","GLOBAL",1)
  SetGlobal("rh#EvaVeldrinTalk","GLOBAL",3)
END

// ---------------------------------------------
// Chapter 4-7 Friendship
// ---------------------------------------------

// Spellhold

IF
    InParty(Myself)
    AreaCheck("AR1512")
    !Global("rh#EvaRomanceActive","GLOBAL",1)
    Global("rh#EvaFriendshipActive","GLOBAL",1)
    Global("rh#EvaSpellholdTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaTalksTimer","GLOBAL",1800)
    IncrementGlobal("rh#EvaSpellholdTalk","GLOBAL",1)
END

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  OR(3)
    AreaCheck("AR1512")
    AreaCheck("AR1513")
    AreaCheck("AR1514")
  RealGlobalTimerExpired("rh#EvaTalksTimer","GLOBAL")
  Global("rh#EvaFriendshipActive","GLOBAL",1)
  !Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaSpellholdTalk","GLOBAL",1)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaSpellholdTalk","GLOBAL",1)
END

// Underdark Talk

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  AreaCheck("AR2100")
  RealGlobalTimerExpired("rh#EvaUnderdarkTimer","GLOBAL")
  Global("rh#EvaFriendshipActive","GLOBAL",1)
  !Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaUnderdarkTalk","GLOBAL",1)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaUnderdarkTalk","GLOBAL",1)
END

// Veldrin

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  Global("PlayerLooksLikeDrow","GLOBAL",1)
  RealGlobalTimerExpired("rh#EvaVeldrinTimer","GLOBAL")
  Global("rh#EvaFriendshipActive","GLOBAL",1)
  !Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaVeldrinTalk","GLOBAL",1)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaVeldrinTalk","GLOBAL",1)
END

// Suldanessellar

IF
    InParty(Myself)
    AreaCheck("AR2800")
    Global("rh#EvaFriendshipActive","GLOBAL",1)
    Global("rh#EvaSuldaTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("rh#EvaSuldaTimer","GLOBAL",450)
    SetGlobal("rh#EvaSuldaTalk","GLOBAL",1)
END

IF  
  InParty("rh#Eva")
  See(Player1)
  !StateCheck("rh#Eva",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !See([ENEMY])
  RealGlobalTimerExpired("rh#EvaSuldaTimer","GLOBAL")
  Global("rh#EvaFriendshipActive","GLOBAL",1)
  Global("rh#EvaSuldaTalk","GLOBAL",1)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaSuldaTalk","GLOBAL",1)
END

// ---------------------------------------------
// Quest
// ---------------------------------------------

IF
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    OR(4)
    Global("rh#EvaPhilomenaTalks","GLOBAL",2)
    Global("rh#EvaPhilomenaTalks","GLOBAL",4)
    Global("rh#EvaPhilomenaTalks","GLOBAL",6)
    Global("rh#EvaKnights","GLOBAL",2)
THEN
RESPONSE #100
    StartDialogueNoSet(Player1)
END

IF
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("rh#EvaPhilomenaTask","GLOBAL",2)
    Global("rh#EvaPhilomenaTalks","GLOBAL",0)
THEN
RESPONSE #100
    RealSetGlobalTimer("rh#EvaPhilomenaTimer","GLOBAL",15)
    IncrementGlobal("rh#EvaPhilomenaTalks","GLOBAL",1)
END

IF
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    RealGlobalTimerExpired("rh#EvaPhilomenaTimer","GLOBAL")
    Global("rh#EvaPhilomenaTask","GLOBAL",2)
    Global("rh#EvaPhilomenaTalks","GLOBAL",1)
THEN
RESPONSE #100
    IncrementGlobal("rh#EvaPhilomenaTalks","GLOBAL",1)
END

IF
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    GlobalLT("rh#EvaPhilomenaTalks","GLOBAL",4)
    AreaCheck("AR2200")
    PartyHasItem("rh#mena")
THEN
RESPONSE #100
    SetGlobal("rh#EvaPhilomenaTalks","GLOBAL",4)
END

IF
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("rh#EvaPhilomenaTask","GLOBAL",10)
    GlobalLT("rh#EvaPhilomenaTalks","GLOBAL",6)
THEN
RESPONSE #100
    SetGlobal("rh#EvaPhilomenaTalks","GLOBAL",6)
END


IF
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    AreaCheck("AR2500")
    Global("rh#EvaPhilomenaTask","GLOBAL",11)
    Global("ElhanFinishedSpeaking","GLOBAL",1)
    Global("rh#EvaKnights","GLOBAL",0)
THEN
  RESPONSE #100
  RealSetGlobalTimer("rh#EvaKnightsTimer","GLOBAL",5)
  IncrementGlobal("rh#EvaKnights","GLOBAL",1)
END

IF
    InParty("rh#Eva")
    See(Player1)
    !StateCheck("rh#Eva",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("rh#EvaPhilomenaTask","GLOBAL",11)
    RealGlobalTimerExpired("rh#EvaKnightsTimer","GLOBAL")
    Global("rh#EvaKnights","GLOBAL",1)
THEN
  RESPONSE #100
  IncrementGlobal("rh#EvaKnights","GLOBAL",1)
END

// ---------------------------------------------
// Romance Kills
// ---------------------------------------------

IF
  OR(2)
  Global("rh#EvaRomanceActive","GLOBAL",1)
  Global("rh#EvaRomanceActive","GLOBAL",2)
  OR(5)
    Global("AerieRomanceActive","GLOBAL",2)
    Global("JaheiraRomanceActive","GLOBAL",2)
    Global("ViconiaRomanceActive","GLOBAL",2)
    Global("NeeraRomanceActive","GLOBAL",2)
    Global("DornRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("rh#EvaRomanceActive","GLOBAL",3)
END

IF
  Global("rh#EvaRomanceActive","GLOBAL",2)
  OR(2)
    Global("AerieRomanceActive","GLOBAL",1)
    Global("AerieRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("AerieRomanceActive","GLOBAL",3)
END

IF
  Global("rh#EvaRomanceActive","GLOBAL",2)
  OR(2)
    Global("JaheiraRomanceActive","GLOBAL",1)
    Global("JaheiraRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("JaheiraRomanceActive","GLOBAL",3)
END

IF  
  Global("rh#EvaRomanceActive","GLOBAL",2)
  OR(2)
    Global("ViconiaRomanceActive","GLOBAL",1)
    Global("ViconiaRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("ViconiaRomanceActive","GLOBAL",3)
END

IF
  Global("rh#EvaRomanceActive","GLOBAL",2)
  OR(2)
    Global("NeeraRomanceActive","GLOBAL",1)
    Global("NeeraRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("NeeraRomanceActive","GLOBAL",3)
END

IF
  Global("rh#EvaRomanceActive","GLOBAL",2)
  OR(2)
    Global("DornRomanceActive","GLOBAL",1)
    Global("DornRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("DornRomanceActive","GLOBAL",3)
END