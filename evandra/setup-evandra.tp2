BACKUP ~evandra/backup~
AUTHOR ~Rhaella @ https://www.gibberlings3.net~
VERSION ~v2.2~
README ~evandra/readme-evandra.html~

ALWAYS
// eet compatibility
ACTION_IF (GAME_IS ~eet~) BEGIN
  OUTER_SET bg2_chapter = 12
END ELSE BEGIN
  OUTER_SET bg2_chapter = 0
END
OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
  OUTER_SET bg2_chapter = bg2_chapter + 1
  OUTER_SPRINT name_source ~bg2_chapter_%i%~
  OUTER_SET EVAL ~%name_source%~ = bg2_chapter
END

  // Borrowed from Edwin Romance: convert strings to UTF-8 for BGEE/BG2EE

  // setup.tra contains in game texts as well as installation texts
  // and need to be converted
  ACTION_DEFINE_ARRAY noconvert BEGIN END
  ACTION_DEFINE_ARRAY reload BEGIN setup END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charset = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/translations~
      // charset_table = charsets // Use HANDLE_CHARSETS built-in rule for charset choice
      noconvert_array = noconvert
      reload_array = reload
  END
END

// Translations
AUTO_TRA ~evandra/translations/%s~
LANGUAGE ~English~
         ~English~
         ~evandra/translations/english/setup.tra~

BEGIN @0 /* Evandra NPC */
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1 /* You must have Throne of Bhaal installed. */

APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
UNLESS ~CD_STATE_NOTVALID~

// Fixing Imoen's dialogue file
ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN
  COPY_EXISTING ~interdia.2da~ ~override~
    SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
    BUT_ONLY_IF_IT_CHANGES
END ELSE BEGIN
  APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
    UNLESS ~BIMOEN2~
END

// fixing kit.ids; creature corrections are down in the creature section
COPY_EXISTING ~kit.ids~ ~override~
  REPLACE_TEXTUALLY ~.*\bGODLATHANDER\b~           ~0x4015 GODLATHANDER~
  BUT_ONLY_IF_IT_CHANGES

// fixing area file
COPY_EXISTING ar0303.are override
  WRITE_ASCII AREA_SCRIPT ar0303 #8
BUT_ONLY

// ---------------------------------------------
// Audio
// ---------------------------------------------

COPY ~evandra/audio/rh#blank.mus~ ~music~

COPY_EXISTING ~songlist.2da~ ~override~
  SET_2DA_ENTRY 0 2 3 ~rh#blank.mus~

LAF HANDLE_AUDIO END

// ---------------------------------------------
// Dialogue & Scripts
// ---------------------------------------------

COMPILE ~evandra/dialogue/intro.d~
COMPILE ~evandra/dialogue/talks.d~
COMPILE ~evandra/dialogue/talkstob.d~
COMPILE EVALUATE_BUFFER ~evandra/dialogue/banters.d~
COMPILE ~evandra/dialogue/quest.d~
COMPILE EVALUATE_BUFFER ~evandra/dialogue/parting.d~
COMPILE EVALUATE_BUFFER ~evandra/dialogue/interjections.d~
COMPILE ~evandra/dialogue/bodhiamb.d~
COMPILE ~evandra/dialogue/wraith.d~

COMPILE EVALUATE_BUFFER ~evandra/scripts/rh#eva.baf~ USING ~evandra/translations/%LANGUAGE%/scripts.tra~
COMPILE EVALUATE_BUFFER ~evandra/scripts/rh#evad.baf~
COMPILE ~evandra/scripts/rh#mena.baf~
COMPILE ~evandra/scripts/rh#drow2.baf~
COMPILE ~evandra/scripts/rh#ev25.baf~
COMPILE ~evandra/scripts/rh#ev25d.baf~
COMPILE ~evandra/scripts/rh#evmes.baf~
COMPILE ~evandra/scripts/rh#evamp.baf~
COMPILE ~evandra/scripts/rh#evct1.baf~
COMPILE ~evandra/scripts/rh#evct2.baf~
COMPILE ~evandra/scripts/rh#evct3.baf~
COMPILE ~evandra/scripts/rh#evct4.baf~
COMPILE ~evandra/scripts/rh#evct5.baf~

EXTEND_TOP ~ar0411.bcs~ ~evandra/scripts/ar0411.baf~ EVALUATE_BUFFER
EXTEND_TOP ~ar6200.bcs~ ~evandra/scripts/ar6200.baf~
EXTEND_BOTTOM ~ar0809.bcs~ ~evandra/scripts/ar0809.baf~
EXTEND_BOTTOM ~ar2100.bcs~ ~evandra/scripts/ar2100.baf~
EXTEND_BOTTOM ~ar2202.bcs~ ~evandra/scripts/ar2202.baf~
EXTEND_BOTTOM ~baldur.bcs~ ~evandra/scripts/baldur.baf~
EXTEND_BOTTOM ~golem.bcs~ ~evandra/scripts/golem.baf~ USING ~evandra/translations/%LANGUAGE%/scripts.tra~
EXTEND_BOTTOM ~rune2.bcs~ ~evandra/scripts/rune2.baf~ USING ~evandra/translations/%LANGUAGE%/scripts.tra~
EXTEND_BOTTOM ~obssol01.bcs~ ~evandra/scripts/obssol01.baf~
EXTEND_BOTTOM ~udlesa2.bcs~ ~evandra/scripts/udlesa2.baf~
EXTEND_BOTTOM ~bodhiamb.bcs~ ~evandra/scripts/bodhiamb.baf~
EXTEND_BOTTOM ~vampamb.bcs~  ~evandra/scripts/vampamb.baf~
EXTEND_BOTTOM ~cleanse.bcs~  ~evandra/scripts/cleanse.baf~
EXTEND_BOTTOM ~cut218g.bcs~ ~evandra/scripts/rh#evct6.baf~

// BG2EE
ACTION_IF GAME_IS ~BG2EE EET~ THEN BEGIN

COMPILE ~evandra/dialogue/bgee.d~
EXTEND_TOP ~ohndenna.bcs~ ~evandra/scripts/ohndenna.baf~ USING ~evandra/translations/%LANGUAGE%/scripts.tra~

ADD_JOURNAL EXISTING TITLE (#74465) @89 USING ~evandra/translations/%LANGUAGE%/intro.tra~
ADD_JOURNAL @45 @52 @84 @150 @152 @197 @215 @217 USING ~evandra/translations/%LANGUAGE%/quest.tra~ 
ADD_JOURNAL @284 @314 @317 USING ~evandra/translations/%LANGUAGE%/quest.tra~ 
ADD_JOURNAL EXISTING TITLE (#74337) @4 @18 @24 USING ~evandra/translations/%LANGUAGE%/bodhiamb.tra~ 
END

ACTION_IF GAME_IS ~EET~ BEGIN
//  EXTEND_TOP ~AR4500.bcs~ ~Evandra\scripts\LockEETEva.baf~
  INCLUDE ~EET/other/EET_functions.tph~ // functions from EET mod
  LAF ~EET_NPC_TRANSITION~
    INT_VAR
      type = 2 // NPC is summonable if met before
    STR_VAR
      dv = "rh#Eva" // death variable of your NPC
      override_SoA = "rh#Eva" // override script of your NPC (SoA)
      override_ToB = "rh#Ev25" // override script of your NPC (ToB)
      dialog_ToB = "rh#Ev25a" // joining dialogue for ToB
      traFile = EVAL "Evandra/translations/%LANGUAGE%/fatespirit.tra"
      string = "@0"
      stringPosDV = "Faldorn"
  END
END ELSE BEGIN
  COMPILE ~evandra/dialogue/fatespirit.d~ USING ~evandra/translations/%LANGUAGE%/fatespirit.tra~
END

// ---------------------------------------------
// Creatures and Images
// ---------------------------------------------

COPY ~evandra/creatures/rh#eva.cre~ ~override~
  SAY NAME1 @2 /* Evandra */
  SAY NAME2 @2 /* Evandra */
  WRITE_ASCII 0x248 ~rh#eva~ #8  //override script
  WRITE_ASCII 0x280 ~rh#Eva~ #32  //DV
  WRITE_ASCII 0x2cc ~rh#eva~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~rh#evas~ #8   //small portrait
  WRITE_ASCII 0x3c ~rh#evam~ #8   //medium portrait
  SAY BIO @3 /* When asked about her past, EVANDRA laughs and points out that very little she could say about Ansalon would mean much of anything to you. She claims to come from an elven kingdom called Qualinesti, though the way that she speaks about it would suggest that she is not on the best of terms with her people. She has considerably more to say about her status as a Wizard of High Sorcery, a prestigious title by all accounts, and one that she admits will take some effort to do without. You get the impression that she is somewhat conflicted about what it might mean to find herself in a new and unfamiliar world. */ 
  SAY MORALE @4 /* I think... I think we'd best get out of here! */
  SAY HAPPY @5 /* You're decent enough company, my friend, and I don't say that lightly. */
  SAY UNHAPPY_ANNOYED @6 /* This is beginning to get tiresome. */
  SAY UNHAPPY_SERIOUS @7 /* I'm beginning to wonder if I don't have better places to be than at your side. */
  SAY UNHAPPY_BREAKING_POINT @8 /* You and I, "friend"... we're finished now. Don't expect to see me again. */ 
  SAY LEADER @9 /* Hm, a wise choice, if I do say so myself. */
  SAY TIRED @10 /* I think it's about time for us to get some rest. */
  SAY BORED @11 /* If I wished to sit around and do nothing, I could have always stayed behind. */
  SAY BATTLE_CRY1 @12 /* Oh, you've crossed the wrong group this time! */
  SAY BATTLE_CRY2 @13 /* This ends here! */
  SAY BATTLE_CRY3 @14 /* Let's see if we can't liven things up a bit! */
  SAY BATTLE_CRY4 ~~ []
  SAY BATTLE_CRY5 ~~ []
  SAY DAMAGE ~~ [rh#ev012]
  SAY DYING ~~ [rh#ev013]
  SAY HURT @16 /* I could use a little help here! */
  SAY AREA_FOREST @17 /* In a place like this, I would almost think I was home again. */
  SAY AREA_CITY @18 /* By Astra's Harp, it seems like this city grows louder every day. */
  SAY AREA_DUNGEON @19 /* We really do find ourselves in the dankest of places, don't we? */
  SAY AREA_DAY @20 /* Another fine day, another fine adventure, I'm sure. */
  SAY AREA_NIGHT @21 /* Ahh, this night sky of yours... I don't know how you make do with just the one moon. */
  SAY SELECT_COMMON1 @22 /* I'm listening. */
  SAY SELECT_COMMON2 @23 /* Did you say something? */
  SAY SELECT_COMMON3 @24 /* You again, hm? */
  SAY SELECT_COMMON4 @25 /* What did you have in mind? */
  SAY SELECT_COMMON5 @26 /* Mmhm? */
  SAY SELECT_COMMON6 @27 /* This had better be interesting. */
  SAY SELECT_ACTION1 @28 /* Whatever you wish. */
  SAY SELECT_ACTION2 @29 /* Well. Alright, then. */
  SAY SELECT_ACTION3 @30 /* I do hope you know what you're doing. */
  SAY SELECT_ACTION4 @31 /* If you're quite sure. */
  SAY SELECT_ACTION5 @32 /* Mm, at your service. */
  SAY SELECT_ACTION6 @33 /* As good a choice as any. */
  SAY SELECT_ACTION7 @34 /* I suppose I can manage that much. */
  SAY CRITICAL_HIT @35 /* Ah, did you see that! */
  SAY CRITICAL_MISS @36 /* Blast it! */
  SAY TARGET_IMMUNE @37 /* A weapon that actually works, if you please! */
  SAY INVENTORY_FULL @38 /* Surely you can find someone more suitable to carry all this. */
  SAY SELECT_RARE1 @39 /* Well, well, well. */
  SAY SELECT_RARE2 @40 /* Interesting. Most interesting. */
  SAY SPELL_DISRUPTED @41 /* I can't cast in these conditions! */
  SAY SET_A_TRAP ~~ []
  SAY HIDDEN_IN_SHADOWS ~~ []
  SAY PICKED_POCKET ~~ []
  ADD_CRE_ITEM ~clck13~ #0 #0 #0 ~IDENTIFIED~ ~armor~
  ADD_CRE_ITEM ~brac03~ #0 #0 #0 ~IDENTIFIED~ ~gloves~
  ADD_CRE_ITEM ~dagg15~ #0 #0 #0 ~IDENTIFIED~ ~weapon1~
  ADD_CRE_ITEM ~slng02~ #0 #0 #0 ~IDENTIFIED~ ~weapon2~
  ADD_CRE_ITEM ~bull01~ #20 #0 #0 ~NONE~ ~quiver1~
  ADD_CRE_ITEM ~bull02~ #20 #0 #0 ~IDENTIFIED~ ~quiver2~

COPY ~evandra/creatures/rh#ev25.cre~ ~override~
  SAY NAME1 @2 /* Evandra */
  SAY NAME2 @2 /* Evandra */
  WRITE_ASCII 0x248 ~rh#ev25~ #8  //override script
  WRITE_ASCII 0x280 ~rh#Eva~ #32  //DV
  WRITE_ASCII 0x2cc ~rh#ev25a~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~rh#evas~ #8   //small portrait
  WRITE_ASCII 0x3c ~rh#evam~ #8   //medium portrait
  SAY BIO @3 /* When asked about her past, EVANDRA laughs and points out that very little she could say about Ansalon would mean much of anything to you. She claims to come from an elven kingdom called Qualinesti, though the way that she speaks about it would suggest that she is not on the best of terms with her people. She has considerably more to say about her status as a Wizard of High Sorcery, a prestigious title by all accounts, and one that she admits will take some effort to do without. You get the impression that she is somewhat conflicted about what it might mean to find herself in a new and unfamiliar world. */ 
  SAY MORALE @4 /* I think... I think we'd best get out of here! */
  SAY HAPPY @5 /* You're decent enough company, my friend, and I don't say that lightly. */
  SAY UNHAPPY_ANNOYED @6 /* This is beginning to get tiresome. */
  SAY UNHAPPY_SERIOUS @7 /* I'm beginning to wonder if I don't have better places to be than at your side. */
  SAY UNHAPPY_BREAKING_POINT @8 /* You and I, "friend"... we're finished now. Don't expect to see me again. */ 
  SAY LEADER @9 /* Hm, a wise choice, if I do say so myself. */
  SAY TIRED @10 /* I think it's about time for us to get some rest. */
  SAY BORED @11 /* If I wished to sit around and do nothing, I could have always stayed behind. */
  SAY BATTLE_CRY1 @12 /* Oh, you've crossed the wrong group this time! */
  SAY BATTLE_CRY2 @13 /* This ends here! */
  SAY BATTLE_CRY3 @14 /* Let's see if we can't liven things up a bit! */
  SAY BATTLE_CRY4 ~~ []
  SAY BATTLE_CRY5 ~~ []
  SAY DAMAGE ~~ [rh#ev012]
  SAY DYING ~~ [rh#ev013]
  SAY HURT @16 /* I could use a little help here! */
  SAY AREA_FOREST @17 /* In a place like this, I would almost think I was home again. */
  SAY AREA_CITY @18 /* By Astra's Harp, it seems like this city grows louder every day. */
  SAY AREA_DUNGEON @19 /* We really do find ourselves in the dankest of places, don't we? */
  SAY AREA_DAY @20 /* Another fine day, another fine adventure, I'm sure. */
  SAY AREA_NIGHT @21 /* Ahh, this night sky of yours... I don't know how you make do with just the one moon. */
  SAY SELECT_COMMON1 @22 /* I'm listening. */
  SAY SELECT_COMMON2 @23 /* Did you say something? */
  SAY SELECT_COMMON3 @24 /* You again, hm? */
  SAY SELECT_COMMON4 @25 /* What did you have in mind? */
  SAY SELECT_COMMON5 @26 /* Mmhm? */
  SAY SELECT_COMMON6 @27 /* This had better be interesting. */
  SAY SELECT_ACTION1 @28 /* Whatever you wish. */
  SAY SELECT_ACTION2 @29 /* Well. Alright, then. */
  SAY SELECT_ACTION3 @30 /* I do hope you know what you're doing. */
  SAY SELECT_ACTION4 @31 /* If you're quite sure. */
  SAY SELECT_ACTION5 @32 /* Mm, at your service. */
  SAY SELECT_ACTION6 @33 /* As good a choice as any. */
  SAY SELECT_ACTION7 @34 /* I suppose I can manage that much. */
  SAY CRITICAL_HIT @35 /* Ah, did you see that! */
  SAY CRITICAL_MISS @36 /* Blast it! */
  SAY TARGET_IMMUNE @37 /* A weapon that actually works, if you please! */
  SAY INVENTORY_FULL @38 /* Surely you can find someone more suitable to carry all this. */
  SAY SELECT_RARE1 @39 /* Well, well, well. */
  SAY SELECT_RARE2 @40 /* Interesting. Most interesting. */
  SAY SPELL_DISRUPTED @41 /* I can't cast in these conditions! */
  SAY SET_A_TRAP ~~ []
  SAY HIDDEN_IN_SHADOWS ~~ []
  SAY PICKED_POCKET ~~ []
  ADD_CRE_ITEM ~clck13~ #0 #0 #0 ~IDENTIFIED~ ~armor~
  ADD_CRE_ITEM ~brac03~ #0 #0 #0 ~IDENTIFIED~ ~gloves~
  ADD_CRE_ITEM ~dagg24~ #0 #0 #0 ~IDENTIFIED~ ~weapon1~
  ADD_CRE_ITEM ~slng04~ #0 #0 #0 ~IDENTIFIED~ ~weapon2~
  ADD_CRE_ITEM ~bull02~ #20 #0 #0 ~NONE~ ~quiver1~
  ADD_CRE_ITEM ~bull03~ #20 #0 #0 ~IDENTIFIED~ ~quiver2~

// Vampire Evandra
COPY_EXISTING ~vampaer.cre~ ~override/rh#evamp.cre~
  SAY NAME1 @2 /* Evandra */
  SAY NAME2 @2 /* Evandra */
  WRITE_EVALUATED_ASCII DIALOG          ~rh#evamp~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~%DEST_RES%~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~%DEST_RES%~ #8
  WRITE_BYTE            0x0237          2
  WRITE_BYTE            0x0275          2
  WRITE_ASCII           0x0034          ~rh#evas~ #8
  WRITE_ASCII           0x003c          ~rh#evam~ #8
  REPLACE_CRE_ITEM ~RH#EVBDY~ #0 #0 #0 ~IDENTIFIED~ ~QITEM1~

ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
  COPY ~evandra/images/rh#eval.bmp~ ~override~

  COPY ~override/rh#eva.cre~ ~override/rh#eva.cre~
  WRITE_ASCII 0x34 ~rh#evaL~ #8   // small portrait
  WRITE_ASCII 0x3c ~rh#evaL~ #8   // large portrait

  COPY ~override/rh#ev25.cre~ ~override/rh#ev25.cre~
  WRITE_ASCII 0x34 ~rh#evaL~ #8   // small portrait
  WRITE_ASCII 0x3c ~rh#evaL~ #8   // large portrait

  COPY ~override/rh#evamp.cre~ ~override/rh#evamp.cre~
  WRITE_ASCII 0x34 ~rh#evaL~ #8   // small portrait
  WRITE_ASCII 0x3c ~rh#evaL~ #8   // large portrait
END ELSE BEGIN
COPY ~evandra/images~ ~override~
END

COPY_EXISTING ~naliames.cre~ ~override/rh#evmes.cre~
  SAY NAME1 @42 /* Messenger */
  SAY NAME2 @42 /* Messenger */
  WRITE_EVALUATED_ASCII DIALOG          ~rh#evmes~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#evmes~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~rh#evmes~ #8
  WRITE_EVALUATED_ASCII SCRIPT_DEFAULT  ~~ #8

COPY ~evandra/creatures/rh#elf.cre~ ~override/rh#mena.cre~
  SAY NAME1 @43  /* Philomena */
  SAY NAME2 @43  /* Philomena */
  WRITE_EVALUATED_ASCII DIALOG          ~rh#mena~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#mena~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~rh#mena~ #8
  WRITE_EVALUATED_ASCII SCRIPT_DEFAULT  ~~ #8

COPY_EXISTING ~uddrow08.cre~ ~override/rh#drow1.cre~
  SAY NAME1 @44  /* Iymril */
  SAY NAME2 @44  /* Iymril */
  WRITE_EVALUATED_ASCII DIALOG          ~rh#drow1~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#drow1~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~rh#drow1~ #8
  WRITE_EVALUATED_ASCII SCRIPT_DEFAULT  ~~ #8

COPY ~evandra/creatures/rh#drow2.cre~ ~override~
  SAY NAME1 @45 /* Tebryn */
  SAY NAME2 @45 /* Tebryn */
  WRITE_EVALUATED_ASCII DIALOG          ~rh#drow2~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#drow2~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~rh#drow2~ #8
  WRITE_EVALUATED_ASCII SCRIPT_DEFAULT  ~~ #8

COPY_EXISTING ~obssol01.cre~ ~override/rh#sol01.cre~
  WRITE_EVALUATED_ASCII DIALOG          ~rh#sol01~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#sol01~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~rh#sol01~ #8
  WRITE_EVALUATED_ASCII SCRIPT_DEFAULT  ~~ #8

COPY_EXISTING ~obssol02.cre~ ~override/rh#sol02.cre~
  WRITE_EVALUATED_ASCII DIALOG          ~rh#sol02~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#sol02~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~rh#sol02~ #8
  WRITE_EVALUATED_ASCII SCRIPT_DEFAULT  ~~ #8

COPY_EXISTING ~obssol03.cre~ ~override/rh#sol03.cre~
  WRITE_EVALUATED_ASCII DIALOG          ~rh#sol03~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#sol03~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~rh#sol03~ #8
  WRITE_EVALUATED_ASCII SCRIPT_DEFAULT  ~~ #8

// Wraith Scene
COPY ~evandra/creatures/rh#elf.cre~ ~override/rh#mara.cre~
  SAY NAME1 @46 /* Marathelassa */
  SAY NAME2 @46 /* Marathelassa */
  WRITE_EVALUATED_ASCII DIALOG          ~rh#mara~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~rh#mara~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~%DEST_RES%~ #8 
  SAY INITIAL_MEETING ~~ []

// ---------------------------------------------
// Items
// ---------------------------------------------

// INCLUDE ~evandra/items/update_item_descriptions_to_bgee.tpa~

COPY_EXISTING ~book87.itm~ ~override/rh#mena.itm~
  SAY NAME1 @47 /* Spellbook */
  SAY NAME2 @47 /* Philomena's Spellbook */
  SAY DESC @48 /* This is the spellbook of Philomena Moonborn, a Silvanesti diviner. A number of powerful spells and rituals known only to her people are to be found within its pages. */

COPY_EXISTING ~miscbl.itm~ ~override/rh#evbdy.itm~
  SAY NAME1 @49 /* Evandra's Body */
  SAY NAME2 @49 /* Evandra's Body */
  SAY DESC @50 /* Evandra was captured by Bodhi and infected with vampirism, forcing you to slay her. There may be some way to revive her, though you don't know what it would be. */

// ---------------------------------------------
// Epilogues
// ---------------------------------------------

// 1. Friendship
COPY ~evandra/epilogue/rh#eva.2da~ ~override/rh#eva1.2da~
REPLACE ~99999~ @51 /* After parting ways with <CHARNAME>, Evandra returned to Suldanessellar, where she lived in peace for a time, forging ties with a people she had thought lost to her forever. She took several apprentices while she lived there, and contributed her skills and magic to the full recovery of the city, though it must be said that she never felt entirely at home in Faerûn. Eventually her desire to see her own world once more grew too strong to resist, and she made her way back to Ansalon, though she found precious little reason to stay. According to legend, she finally took up a life of planeswalking, though that is a tale for another day. */

// 2. Romance, no marriage
COPY ~evandra/epilogue/rh#eva.2da~ ~override/rh#eva2.2da~
REPLACE ~99999~ @52 /* Shortly after the defeat of Melissan, Evandra and <CHARNAME> returned to Suldanessellar, where they were warmly welcomed by their elven kin. They stayed there for some time, contributing as they could to the full recovery of the city, but eventually began to grow restless. They were spotted from time to time in the Western Heartlands, but for a period of several decades, they traveled the planes instead, settling for a time on Evandra's homeworld of Ansalon. Evandra returned to the Wizards of High Sorcery for several years, and briefly considered taking up leadership of the Order of the Red Robes, but the birth of their first daughter saw an end to that plan. Not wishing to see their children grow up exiled, Evandra and <CHARNAME> finally returned to Faerûn, and it is said that they eventually settled in Evermeet, though of their life on the island of the elves, no mortal tale tells. */

// 3. Romance, marriage in Suldanessellar
COPY ~evandra/epilogue/rh#eva.2da~ ~override/rh#eva3.2da~
REPLACE ~99999~ @53 /* Shortly after the defeat of Melissan, Evandra and <CHARNAME> returned to Suldanessellar, where they were warmly welcomed by their elven kin. The preparations for their wedding were a festive affair for all involved, and proved a happy moment not only for themselves and their companions, but for the grief-stricken populace in general. They stayed there for some time afterwards, contributing as they could to the full recovery of the city, but eventually began to grow restless. They were spotted from time to time in the Western Heartlands, but for a period of several decades, they traveled the planes instead, settling for a time on Evandra's homeworld of Ansalon. Evandra returned to the Wizards of High Sorcery for several years, and briefly considered taking up leadership of the Order of the Red Robes, but the birth of their first daughter saw an end to that plan. Not wishing to see their children grow up exiled, Evandra and <CHARNAME> finally returned to Faerûn, and it is said that they eventually settled in Evermeet, though of their life on the island of the elves, no mortal tale tells. */

// 4. Romance, marriage in Amn
COPY ~evandra/epilogue/rh#eva.2da~ ~override/rh#eva4.2da~
REPLACE ~99999~ @54 /* Shortly after the defeat of Melissan, Evandra and <CHARNAME> returned to Athkatla, where they were married in a small, private ceremony attended only by their closest friends. For several years, they split their time between the human nation of Amn and the elven city of Suldanessellar, contributing as they could to the full recovery of the city. Eventually, however, they began to grow restless, as if even two realms were no longer enough to hold their attention. They were spotted from time to time in the Western Heartlands, but for a period of several decades, they traveled the planes instead, settling for a time on Evandra's homeworld of Ansalon. Evandra returned to the Wizards of High Sorcery for several years, and briefly considered taking up leadership of the Order of the Red Robes, but the birth of their first daughter saw an end to that plan. Not wishing to see their children grow up exiled, Evandra and <CHARNAME> finally returned to Faerûn, and it is said that they eventually settled in Evermeet, though of their life on the island of the elves, no mortal tale tells. */

// 5. Romance, PC ascends
COPY ~evandra/epilogue/rh#eva.2da~ ~override/rh#eva5.2da~
REPLACE ~99999~ @55 /* After <CHARNAME>'s ascension to godhood, Evandra returned to Suldanessellar, where she lived in peace for a time, forging ties with a people she had thought lost to her forever. She took several apprentices while she lived there, and contributed her skills and magic to the full recovery of the city, though found that she could never quite forget her lost love. Eventually her dissatisfaction with Faerûn grew too strong to resist, and she made her way back to Ansalon, though she found precious little reason to stay. According to legend, she finally took up a life of planeswalking, though that is a tale for another day. */

APPEND ~pdialog.2da~
  ~rh#Eva rh#evap rh#evaj rh#evad rh#ev25p rh#ev25j rh#ev25d rh#ev25~
UNLESS ~rh#eva~

APPEND ~interdia.2da~
  ~rh#Eva brh#eva brh#ev25~
UNLESS ~rh#Eva~


// ---------------------------------------------
// Crossmod
// ---------------------------------------------

BEGIN @56 /* Crossmod Content */
REQUIRE_COMPONENT ~evandra.tp2~ ~0~ @57 /* Please install Evandra before additional content */

// Adrian Material
ACTION_IF FILE_EXISTS_IN_GAME ~rh#adr.dlg~ AND FILE_EXISTS_IN_GAME ~rh#eva.dlg~
THEN BEGIN
  PRINT @58 /* Adding banters between Adrian and Evandra... */
COMPILE ~evandra/crossmod/adrian.d~ USING ~evandra/translations/%LANGUAGE%/crossmod.tra~
EXTEND_BOTTOM ~rh#adrd.bcs~ ~evandra/crossmod/adrian_rh#adrd.baf~
END

// Isra Material
ACTION_IF FILE_EXISTS_IN_GAME ~rh#eva.dlg~ AND FILE_EXISTS_IN_GAME ~rh#isr.dlg~
THEN BEGIN
  PRINT @59 /* Adding banters between Evandra and Isra... */
COMPILE ~evandra/crossmod/isra.d~ USING ~evandra/translations/%LANGUAGE%/crossmod.tra~
END

// Evandra-Kulyok's Xan Material
ACTION_IF FILE_EXISTS_IN_GAME ~rh#eva.dlg~ AND FILE_EXISTS_IN_GAME ~o#xan25.dlg~
THEN BEGIN
  PRINT @60 /* Adding banters between Evandra and Kulyok's Xan... */
  COMPILE ~evandra/crossmod/xan.d~ USING ~evandra/translations/%LANGUAGE%/crossmod.tra~
END